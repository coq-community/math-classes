
import os, glob, string

nodes = ['.']
dirs = []
vs = []

while nodes:
  node = nodes.pop()
  b = os.path.basename(node)
  if node.endswith('.v') and node != "./misc/benchmarks.v":
    vs += [node]
  if os.path.isdir(node) and node != "./broken":
    dirs += [node]
    nodes += glob.glob(node + '/*')

Rs = ' -R . "" '

coqc = 'coqc -q ' + Rs + ' $SOURCE'

env = DefaultEnvironment(ENV = os.environ)

def add_glob(target, source, env):
  base, _ = os.path.splitext(str(target[0]))
  target.append(base + ".glob")
  return target, source

env.Append(BUILDERS =
  {'Coq' : Builder(
    action = coqc,
    suffix = '.vo',
    src_suffix = '.v',
    emitter = add_glob)})

vos = globs = []
for node in vs:
  vo, glob = env.Coq(node)
  vos += [vo]
  globs += [glob]

env.Command(env.Dir('coqdoc'), vs + vos + globs + [File('../tools/coqdoc.css.diff')],
  [ Delete('coqdoc')
  , Mkdir('coqdoc')
  , 'coqdoc -utf8 --toc -g --no-lib-name -d $TARGET' + Rs + ' '.join(vs)
  , 'patch --backup $TARGET/coqdoc.css ../tools/coqdoc.css.diff'])

os.system('coqdep ' + Rs + ' '.join(vs) + ' > deps')
ParseDepends('deps')

Default('implementations', 'theory', 'categories', 'orders', 'varieties', 'misc')

open('coqidescript', 'w').write('#!/bin/sh\ncoqide ' + Rs.replace('"', '\\"') + ' $@ \n')
os.chmod('coqidescript', 0755)

